buildscript {
    repositories {
        maven {
            url "https://maven.eveoh.nl/content/groups/public"
        }
    }


    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.5'
    }
}

apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'war'

group = 'nl.eveoh.mytimetable'
version = '2.2.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.jacksonVersion = '2.2.3'
ext.blackboardVersion = '3000.1.0-rel.52'
ext.logbackVersion = '1.0.13'
ext.slf4jVersion = '1.7.5'
ext.guavaVersion = '16.0.1'

war {
    // Replace version in Blackboard Manifest
    inputs.property('version', archiveVersion)

    filesMatching("**/bb-manifest.xml") {
        expand version: archiveVersion
    }

    manifest {
        attributes(
                'Implementation-Title': 'MyTimetable Blackboard building block',
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor': 'Eveoh',
                'Provider': 'Gradle'
        )
    }

    manifest.from(jar.manifest)
}

configurations {
    // Exclude commons-logging dependency, the SLF4J bridge will take care of this
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

repositories {
    maven {
        url "https://maven.eveoh.nl/content/groups/public"
    }
}

tomcat {
    jasper {
        validateTld = false
        webXmlFragment = file("$webAppDir/WEB-INF/generated_web.xml")
        addWebXmlMappings = true
    }
}

dependencies {
    def tomcatVersion = '8.5.0'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
            "blackboard.platform:bb-taglibs:${blackboardVersion}",
            "blackboard.platform:bb-platform:${blackboardVersion}",
            "javax.servlet:jstl:1.2"

    providedCompile 'javax.servlet:servlet-api:2.5'
    providedCompile 'javax.servlet:jsp-api:2.0'

    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    implementation 'org.apache.commons:commons-lang3:3.1'
    implementation "com.google.guava:guava:${guavaVersion}"

    implementation 'nl.eveoh.mytimetable:mytimetable-api-client:1.3.7'

    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    // Blackboard provided libs
    providedCompile "blackboard.platform:bb-cms-admin:${blackboardVersion}"
    providedCompile "blackboard.platform:bb-platform:${blackboardVersion}"
    providedCompile "blackboard.platform:bb-taglibs:${blackboardVersion}"

    providedCompile 'javax.servlet:jstl:1.2'
}

task compileJsps(type: JavaCompile) {
    group = 'build'
    description = 'Translates and compiles JSPs'
    classpath = configurations.tomcat + sourceSets.main.output + sourceSets.main.runtimeClasspath
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    destinationDir = file("src/main/webapp/WEB-INF/classes")
    source = file("build/jasper/org/apache/jsp")
    dependsOn classes
    dependsOn tomcatJasper
}

war.dependsOn compileJsps
