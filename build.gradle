apply plugin: 'war'

def getGitRevision = {
    try {
        def git = 'git rev-parse --short HEAD'.execute()
        def status = git.waitFor()
        assert status == 0, "${git.err.text}"
        return git.text.trim()
    }
    catch (AssertionError a) {}
    catch (Exception e) {}
    'unknown'
}

group = 'nl.eveoh.mytimetable.blackboard'
version = '2.0.' + getGitRevision()

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext.customer = null

ext.jacksonVersion = '2.2.3'
ext.blackboardVersion = '9.1.120113.0'

war {
    // Replace version in Blackboard Manifest
    inputs.property('version', version)

    filesMatching("**/bb-manifest.xml") {
        expand version: version
    }

    manifest {
        attributes(
                'Implementation-Title': 'MyTimetable Blackboard building block',
                'Implementation-Version': (!customer) ? version : "${version}-${customer}",
                'Implementation-Vendor': 'Eveoh',
                'Provider': 'Gradle'
        )
    }

    manifest.from(jar.manifest)

    classifier = customer
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.blackboard.com/content/repositories/releases"
    }
    maven {
        url "http://dl.eveoh.nl/maven2"
    }
}

dependencies {
    providedCompile 'javax.servlet:servlet-api:2.5'

    compile 'log4j:log4j:1.2.16'
    compile 'org.slf4j:slf4j-log4j12:1.6.4'

    compile 'org.apache.commons:commons-lang3:3.1'

    compile 'nl.eveoh.mytimetable.apiclient:mytimetable-api-client:1.0.3'

    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    // Blackboard provided libs
    providedCompile "blackboard.platform:bb-cms-admin:${blackboardVersion}"
    providedCompile "blackboard.platform:bb-platform:${blackboardVersion}"
    providedCompile "blackboard.platform:bb-taglibs:${blackboardVersion}"

    providedCompile 'javax.servlet:jstl:1.2'
}