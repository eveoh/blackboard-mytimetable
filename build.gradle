apply plugin: 'war'

def getGitRevision = {
    try {
        def git = 'git rev-parse --short HEAD'.execute()
        def status = git.waitFor()
        assert status == 0, "${git.err.text}"
        return git.text.trim()
    }
    catch (AssertionError a) {}
    catch (Exception e) {}
    'unknown'
}

ext.customer = null

group = 'nl.eveoh.mytimetable'
version = '2.0.' + getGitRevision()

sourceCompatibility = 1.5
targetCompatibility = 1.5

war {
    // Replace version in Blackboard Manifest
    inputs.property('version', version)

    filesMatching("**/bb-manifest.xml") {
        expand version: version
    }

    manifest {
        attributes(
                'Implementation-Title': 'MyTimetable Blackboard building block',
                'Implementation-Version': (!customer) ? version : "${version}-${customer}",
                'Implementation-Vendor': 'Eveoh',
                'Provider': 'Gradle'
        )
    }

    manifest.from(jar.manifest)

    classifier = customer
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://www.eveoh.nl/files/maven2"
    }
    maven {
        credentials {
            username eveohMavenUser
            password eveohMavenPassword
        }
        url "https://maven.eveoh.nl"
    }
}

dependencies {
    providedCompile 'javax.servlet:servlet-api:2.5'

    compile 'log4j:log4j:1.2.16'
    compile 'org.slf4j:slf4j-log4j12:1.6.4'

    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'org.apache.httpcomponents:httpclient:4.3.1'

    compile 'com.fasterxml.jackson.core:jackson-core:2.2.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.3'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.3'

    // Blackboard provided libs
    providedCompile 'nl.eveoh.blackboard:bb-cms-admin:9.1.452.0'
    providedCompile 'nl.eveoh.blackboard:bb-platform:9.1.452.0'
    providedCompile 'nl.eveoh.blackboard:bb-taglibs:9.1.452.0'

    providedCompile 'javax.servlet:jstl:1.2'
}