apply plugin: 'war'

def getGitRevision = {
    try {
        def git = 'git rev-parse --short HEAD'.execute()
        def status = git.waitFor()
        assert status == 0, "${git.err.text}"
        return git.text.trim()
    }
    catch (AssertionError a) {}
    catch (Exception e) {}
    'unknown'
}

ext.customer = null

group = 'nl.eveoh.mytimetable'
version = '1.1.5.' + getGitRevision()

sourceCompatibility = 1.5
targetCompatibility = 1.5

war {
    // Replace version in Blackboard Manifest
    inputs.property('version', version)

    filesMatching("**/bb-manifest.xml") {
        expand version: version
    }

    manifest {
        attributes(
                'Implementation-Title': 'MyTimetable Blackboard building block',
                'Implementation-Version': (!customer) ? version : "${version}-${customer}",
                'Implementation-Vendor': 'Eveoh',
                'Provider': 'Gradle'
        )
    }

    manifest.from(jar.manifest)

    classifier = customer
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://www.eveoh.nl/files/maven2"
    }
    maven {
        credentials {
            username eveohMavenUser
            password eveohMavenPassword
        }
        url "https://maven.eveoh.nl"
    }
}

dependencies {
    providedCompile 'javax.servlet:servlet-api:2.5'

    compile 'log4j:log4j:1.2.16'
    compile 'org.slf4j:slf4j-log4j12:1.6.4'

    compile 'org.apache.commons:commons-lang3:3.1'

    // Blackboard provided libs
    compile 'nl.eveoh.blackboard:bb-cms-admin:9.1.452.0'
    compile 'nl.eveoh.blackboard:bb-platform:9.1.452.0'
    compile 'nl.eveoh.blackboard:bb-taglibs:9.1.452.0'
    providedCompile 'javax.servlet:jstl:1.2'

    // CXF with HTTP transport, JaxWS frontend and Aegis databinding
    // Do not include Spring for now
    compile('org.apache.cxf:cxf-rt-transports-http:2.5.1') {
        exclude group: 'org.springframework'
    }
    compile('org.apache.cxf:cxf-rt-frontend-jaxws:2.5.1') {
        exclude group: 'org.springframework'
    }
    compile('org.apache.cxf:cxf-rt-databinding-aegis:2.5.1') {
        exclude group: 'org.springframework'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}